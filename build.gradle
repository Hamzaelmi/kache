buildscript {
    ext.kotlin_version = '1.2.10'
    ext.junit_version = '4.12'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

group 'com.yundom'
version '0.0.2'

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            from components.java
            groupId 'com.yundom'
            artifactId 'kache'
            version '0.0.2'
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER_NAME") ?: ''
    key = System.getenv("BINTRAY_API_KEY") ?: ''
    publications = ['BintrayPublication']
    pkg {
        repo = 'Kache'
        name = 'Kache'
        desc = 'A lightweight cache library written in Kotlin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/yundom/kache'
        version {
            name = 'v0.0.2'
            released = new Date()
            vcsTag = '0.0.2'
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testCompile "junit:junit:$junit_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}